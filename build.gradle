plugins {
    id 'fabric-loom' version "${loom_version}" apply(false)
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    base {
        archivesName = "${mod_id}-${project.name}-${minecraft_version}"
    }

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            attributes([
                    'Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : project.jar.archiveVersion,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.jar.archiveVersion,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'               : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Built-On-Minecraft'      : minecraft_version
            ])
        }
    }

    sourcesJar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
    }

    repositories {
        maven {
            name = 'Parchment'
            url = 'https://maven.parchmentmc.org'
            content {
                includeGroupAndSubgroups 'org.parchmentmc.data'
            }
        }
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
            content {
                includeGroupAndSubgroups 'net.fabricmc.fabric-api'
            }
        }
        maven {
            name = "Cloth Config"
            url "https://maven.shedaniel.me/"
            content {
                includeGroupAndSubgroups 'me.shedaniel'
                includeGroupAndSubgroups 'dev.architectury'
            }
        }
        maven {
            name = "Mod Menu"
            url 'https://maven.terraformersmc.com/releases'
            content {
                includeGroupAndSubgroups 'com.terraformersmc'
            }
        }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
            content {
                includeGroupAndSubgroups 'maven.modrinth'
            }
        }

        // Non-specific
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }
        maven {
            name = 'Mojang and Mixins'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    processResources {
        var replaceProperties = [
                mod_name: mod_name,
                resource_pack_format_number: resource_pack_format_number,
                data_pack_format_number: data_pack_format_number,
                mod_id: mod_id,
                major_fml_version: major_fml_version,
                version: project.version,
                description: project.description,
                mod_author: mod_author,
                contributors: contributors,
                license: license,
                icon: icon,
                neoforge_version: neoforge_version,
                fabric_loader_version: fabric_loader_version,
                fabric_version: fabric_version,
                minecraft_version: minecraft_version,
                next_minecraft_version: next_minecraft_version,
                github_name: github_name
        ]
        inputs.properties replaceProperties

        filesMatching(['META-INF/mods.toml','fabric.mod.json','pack.mcmeta','*.mixins.json']) {
            expand replaceProperties + [project: project]
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {

        enabled = false
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = project.group
                artifactId = base.archivesName.get()
                version project.version
                from components.java

                pom {
                    name = project.mod_name
                    description = project.description
                    url = "https://github.com/thomasglasser/${github_name}/wiki"
                    licenses {
                        license {
                            name = project.license
                            url = project.license_url
                        }
                    }
                    developers {
                        developer {
                            id = project.mod_author
                            name = project.mod_author
                        }
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = "https://github.com/thomasglasser/${github_name}/issues"
                    }
                    scm
                            {
                                connection = "scm:git:git://github.com/thomasglasser/${github_name}.git"
                                developerConnection = "scm:git:ssh://github.com:thomasglasser/${github_name}.git"
                                url = "https://github.com/thomasglasser/${github_name}/tree/${minecraft_version}"
                            }
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username = "$usr"
                    password = "$pwd"
                }

                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications
    }
}
