plugins {
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.+'
    id 'java-library'
}

subsystems {
    parchment {
        // The Minecraft version for which the Parchment mappings were created.
        // This does not necessarily need to match the Minecraft version your mod targets
        // Defaults to the value of Gradle property neogradle.subsystems.parchment.minecraftVersion
        minecraftVersion = minecraft_version

        // The version of Parchment mappings to apply.
        // See https://parchmentmc.org/docs/getting-started for a list.
        // Defaults to the value of Gradle property neogradle.subsystems.parchment.mappingsVersion
        mappingsVersion = parchment_version
    }
}

// Automatically enable neoforge AccessTransformers if the file exists
// This location is hardcoded in FML and can not be changed.
// https://github.com/neoforged/FancyModLoader/blob/a952595eaaddd571fbc53f43847680b00894e0c1/loader/src/main/java/net/neoforged/fml/loading/moddiscovery/ModFile.java#L118
if (file('src/main/resources/META-INF/accesstransformer.cfg').exists()) {
    minecraft.accessTransformers.file file('src/main/resources/META-INF/accesstransformer.cfg')
}
runs {
    configureEach {
        modSource project.sourceSets.main
    }
    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('../common/src/generated/resources/').getAbsolutePath(), '--existing', file('../common/src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

jarJar.enable()
def jarJarRangeAttribute = Attribute.of("jarJarRange", String.class);
configurations {
    include
    jarJar {
        dependencies.addAllLater(project.provider {
            project.configurations.include.allDependencies.collect {
                def newDep = it.copy()
                if (newDep instanceof ModuleDependency) {
                    newDep.attributes {
                        it.attribute(jarJarRangeAttribute, "[$newDep.version,)" as String)
                    }
                    newDep.setTransitive(false)
                } else {
                    throw new RuntimeException("Attempted to 'include' non-module dependency")
                }
                newDep
            }
        })
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"
    implementation project(":common")

    include implementation("dev.thomasglasser.tommylib:tommylib-neoforge-${minecraft_version}:${tommylib_version}") {
        transitive = false
    }

    // Runtime
//    runtimeOnly "maven.modrinth:jade:${jade_version}-neoforge"
//    runtimeOnly "me.shedaniel:RoughlyEnoughItems-neoforge:$rei_version"
//    runtimeOnly "me.shedaniel.cloth:cloth-config-neoforge:$cloth_config_version"
}

// NeoGradle compiles the game, but we don't want to add our common code to the game's code
Spec<Task> notNeoTask = { Task it -> !it.name.startsWith("neo") } as Spec<Task>

tasks.withType(JavaCompile).matching(notNeoTask).configureEach {
    source(project(":common").sourceSets.main.allSource)
}

tasks.withType(Javadoc).matching(notNeoTask).configureEach {
    source(project(":common").sourceSets.main.allJava)
}

tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

tasks.withType(ProcessResources).matching(notNeoTask).configureEach {
    from project(":common").sourceSets.main.resources
}

tasks.named('jarJar') {
    archiveClassifier = ''
}